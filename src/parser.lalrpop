use std::str::FromStr;
use systemf::{Type, Expr};
use std::rc::Rc;

#[LALR]
grammar;

pub expr: Expr = {
    Expression,
};

Expression: Expr = {
    "lambda" <id:Identifier> ":" <t:Typ> "." <e:Expression> => Expr::Lam(id, t, Box::new(e)),
    "Lambda" <id:Identifier> "." <e:Expression> => Expr::TLam(id, Box::new(e)),
    "let" <id:Identifier> ":" <t:Typ> "=" <e1:Expression> "in" <e2:Expression> => Expr::Let(id, t, Box::new(e1), Box::new(e2)),
    "Let" <id:Identifier> "=" <t:Typ> "in" <e:Expression> => Expr::TLet(id, t, Box::new(e)),
    Factor
};

Factor: Expr = {
    <l:Factor> <r:Term> => Expr::App(Box::new(l), Box::new(r)),
    <l:Factor> "[" <r:Typ> "]" => Expr::TApp(Box::new(l), r),
    Term,
};

Term: Expr = {
    <id:Identifier> => Expr::Var(id),
    "(" <e:Expression> ")" => e,
};

Typ: Box<Type> = {
    "forall" <id:Identifier> "." <t:Typ> => Box::new(Type::Forall(id, t)),
    TFactor,
};

TFactor: Box<Type> = {
    <t1:TTerm> "->" <t2:TFactor> => Box::new(Type::Fun(t1, t2)),
    TTerm,
};

TTerm: Box<Type> = {
    <id:Identifier> => Box::new(Type::Var(id)),
    "(" <t:Typ> ")" => t,
};

Identifier: String = {
    r"[a-zA-Z][a-zA-Z0-9]*" => String::from(<>),
};